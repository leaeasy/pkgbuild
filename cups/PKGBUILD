# $Id: PKGBUILD 231222 2015-02-10 16:00:40Z andyrtr $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase="cups"
pkgname=('libcups')
pkgver=2.2.7
pkgrel=4
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.cups.org/"
makedepends=('libtiff>=4.0.0' 'libpng>=1.5.7' 'acl' 'pam' 'xdg-utils' 'krb5' 'gnutls'
             'bc' 'gzip' 'autoconf' 'dbus' 
             'hicolor-icon-theme' 'systemd' 'inetutils' )

source=(https://github.com/apple/cups/releases/download/v${pkgver}/cups-${pkgver}-source.tar.gz)
sha256sums=('f589bb7d5d1dc3aa0915d7cf2b808571ef2e1530cd1a6ebe76ae8f9f4994e4f6')

prepare() {

  cd ${pkgbase}-${pkgver}

  # Rebuild configure script for not zipping man-pages.
  aclocal -I config-scripts
  autoconf -I config-scripts
}


build() {
  cd ${pkgbase}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var  --libdir=/usr/lib \
      --disable-ldap --disable-raw-printing --disable-gssapi --disable-dbus \
      --enable-ssl=yes --enable-threads --enable-libusb=no \
      --disable-avahi --disable-dnssd \
      --with-optim="$CFLAGS"
  make libs
}


package() {
pkgdesc="The CUPS Printing System - client libraries and headers"
depends=('gnutls' 'libtiff>=4.0.0' 'libpng>=1.5.7' 'krb5')

  cd ${pkgbase}-${pkgver}
  make BUILDROOT=${pkgdir} install-headers install-libs
  
}
