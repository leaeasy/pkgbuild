# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gvfs
pkgver=1.40.2
pkgrel=1
pkgdesc="Virtual filesystem implementation for GIO"
url="https://wiki.gnome.org/Projects/gvfs"
arch=(x86_64)
license=(LGPL)
depends=(dconf fuse libarchive libsoup udisks2 libsecret
         libgudev gcr psmisc)
makedepends=(dbus libgphoto2 docbook-xsl gtk3
             git gtk-doc python meson openssh)
groups=(gnome)
_commit=b7dc2daf2af667f1816ecdb8561c5a21beca85a8  # tags/1.40.2^0
source=("git+https://gitlab.gnome.org/GNOME/gvfs.git#commit=$_commit"
        gvfsd.hook)
sha256sums=('SKIP'
            '478b9cf7b4c242959fc640dbf0cd4935f16c59b81f5828a3af102d608d7a9d72')

prepare() {
  cd $pkgbase
}

pkgver() {
  cd $pkgbase
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson $pkgbase build -D man=false -D afc=false -D afp=false \
      -D cdda=false -D dnssd=false -D goa=false -D google=false -D gphoto=false \
      -D nfs=false -D smb=false -D mtp=false -D bluray=false
  ninja -C build
}


package() {
  options=(!emptydirs)
  DESTDIR="$pkgdir" meson install -C build
  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 *.hook
  install -d -o root -g 102 -m 750 "$pkgdir/usr/share/polkit-1/rules.d"
}

