# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Michael Kanis <mkanis@gmx.de>

pkgname=libmutter2
pkgver=3.28.3
pkgrel=1
pkgdesc='A window manager library for GNOME'
url='https://gitlab.gnome.org/GNOME/mutter'
arch=(x86_64)
license=(GPL)
depends=(
  atk cairo dconf fontconfig gdk-pixbuf2 glib2 gnome-desktop gnome-settings-daemon
  gobject-introspection-runtime gtk3 json-glib mesa pango startup-notification
  tdb upower
)
makedepends=(
  git gobject-introspection intltool
)
source=(
    "git+https://gitlab.gnome.org/GNOME/mutter.git#tag=${pkgver}"
    "fix-schemas-version-above-3.30.patch"
)
sha256sums=(SKIP SKIP)

prepare() {
  cd mutter
  patch -Np1 -i ${srcdir}/fix-schemas-version-above-3.30.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd mutter

  ./configure \
    --prefix='/usr' \
    --includedir='/usr/include/mutter-2' \
    --disable-gtk-doc \
    --disable-schemas-compile \
    --disable-remote-desktop \
    --disable-static \
    --enable-compile-warnings='minimum' \
    --enable-egl-device

  make
}

package() {
  make DESTDIR="${pkgdir}" -C mutter install
  rm -rf ${pkgdir}/usr/{bin,lib/mutter/plugins/default.so,libexec,share}
}

# vim: ts=2 sw=2 et:
