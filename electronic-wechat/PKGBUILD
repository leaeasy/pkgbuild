# Contributor: wenLiangcan <boxeed at gmail dot com>
# Maintainer: hexchain <i at hexchain.org>

pkgname=electronic-wechat
pkgver=2.3.1~2~g48a4e90
pkgrel=1
pkgdesc="A better WeChat client"
arch=('x86_64')
url="https://github.com/geeeeeeeeek/electronic-wechat"
license=('custom')
depends=('electron')
optdepends=('libappindicator-gtk3: fix broken tray icon in KDE')
makedepends=('git' 'npm')
source=(
    "git+https://github.com/kooritea/electronic-wechat"
    'electronic-wechat.desktop'
    'remove-bundle-electron.patch'
    'set-config-to-xdg-spec.patch'
    'electronic-wechat.sh'
)

sha256sums=('SKIP'
            '56c0db46c3b9fc31ac16265d0346ef47a6422392607bcce954e0f550894475be'
            'e6db14369ebd0071f9c9302aab0eed07fd4eff67375c522e03b3b6eb800f2891'
            'SKIP'
            'dddbd40a98fdfa47728fadaceda35a5ac38f59fd1be4fde2cbdaaf309d4e6bf1')

pkgver() {
  cd "$srcdir/$pkgname"
  echo "$(git describe HEAD --tags | sed 's|v||' | sed 's|-|~|g')"
}


prepare() {
    cd "$srcdir/$pkgname"
    patch -p1 < "$srcdir/remove-bundle-electron.patch"
    patch -p1 < "$srcdir/set-config-to-xdg-spec.patch"
}

build() {
    cd "$srcdir/$pkgname"
    npm install
}

package() {
    cd "$srcdir"
    install -Dm644 electronic-wechat.desktop -t "$pkgdir/usr/share/applications/"

    cd "$srcdir/$pkgname"
    install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -Dm644 assets/icon.png "$pkgdir/usr/share/icons/hicolor/512x512/apps/electronic-wechat.png"

    #for size in 16 24 32 48 64 72 128 256; do
    #    target="$pkgdir/usr/share/icons/hicolor/${size}x${size}/apps/"
    #    mkdir -p $target
    #    convert assets/icon.png -resize ${size}x${size} "$target/$pkgname.png"
    #done

    cd "$srcdir/$pkgname/"
    rm -rf scripts
    mkdir -p "$pkgdir/usr/lib/$pkgname/"
    cp -r --no-preserve='ownership' -- * "$pkgdir/usr/lib/$pkgname/"
    install -Dm755 "$srcdir/electronic-wechat.sh" "$pkgdir/usr/bin/electronic-wechat"
}

