# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: sh4nks <sh4nks7@gmail.com

pkgname=lightdm-pantheon-greeter
pkgver=3.3.1
pkgrel=1
pkgdesc='Pantheon greeter for LightDM'
arch=('x86_64')
url='https://github.com/elementary/greeter'
license=('GPL')
groups=('pantheon-unstable')
depends=('cairo' 'clutter' 'clutter-gtk' 'gdk-pixbuf2' 'glib2' 'glibc' 'gtk3'
         'libgee' 'libgl' 'libx11' 'lightdm'
         'granite' 'wingpanel')
makedepends=('meson' 'git' 'granite' 'libxfixes' 'vala' 'wingpanel')
_tagver=3.3.1
install='lightdm-pantheon-greeter.install'
source=("lightdm-pantheon-greeter::git+https://github.com/elementary/greeter.git#tag=$_tagver"
    '0002-remove-unused-gsettings-crash.patch')
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd lightdm-pantheon-greeter
  echo "$(git describe HEAD --tags | sed 's|-|~|g')"
}

prepare() {
  cd lightdm-pantheon-greeter
  patch -Np1 -i ${srcdir}/0002-remove-unused-gsettings-crash.patch
}

build() {
  mkdir -p build
  cd build
  arch-meson ../lightdm-pantheon-greeter
  ninja
}

package() {
  cd build

  DESTDIR="${pkgdir}" ninja install
}

# vim: ts=2 sw=2 et:
