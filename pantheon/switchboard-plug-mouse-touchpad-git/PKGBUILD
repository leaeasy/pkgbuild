# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-mouse-touchpad
pkgver=2.2.0~34~g7476c5c
pkgrel=1
pkgdesc='Switchboard Mouse & Touchpad Plug'
arch=('x86_64')
url='https://github.com/elementary/switchboard-plug-mouse-touchpad'
license=('GPL3')
groups=('pantheon-unstable')
depends=('glib2' 'glibc' 'gtk3' 'libgee'
         'granite' 'switchboard')
makedepends=('git' 'granite' 'meson' 'switchboard' 'vala')
source=('git+https://github.com/elementary/switchboard-plug-mouse-touchpad.git')
sha256sums=('SKIP')

pkgver() {
  cd switchboard-plug-mouse-touchpad

  echo "$(git describe HEAD --tags | sed 's|-|~|g')"
}

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  arch-meson ../switchboard-plug-mouse-touchpad
  ninja
}

package() {
  cd build

  DESTDIR="${pkgdir}" ninja install
}

# vim: ts=2 sw=2 et:
