# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-desktop
pkgver=0.2.6~r1035
pkgrel=1
pkgdesc='Switchboard Desktop Plug'
arch=('x86_64')
url='https://github.com/elementary/switchboard-plug-pantheon-shell'
license=('GPL3')
groups=('pantheon-unstable')
depends=('gdk-pixbuf2' 'glib2' 'glibc' 'gnome-desktop' 'gtk3' 'libgee'
         'libgexiv2' 'plank'
         'libgranite.so' 'libswitchboard-2.0.so')
makedepends=('meson' 'git' 'granite' 'libgexiv2' 'switchboard' 'vala')
source=('switchboard-plug-desktop::git+https://github.com/elementary/switchboard-plug-pantheon-shell.git')
sha256sums=('SKIP')

pkgver() {
  cd switchboard-plug-desktop

  echo "0.2.6~r$(git rev-list --count HEAD)"
}

prepare() {
  cd switchboard-plug-desktop

}

build() {
  mkdir -p build
  cd build
  arch-meson ../switchboard-plug-desktop
  ninja
}

package() {
  cd build

  DESTDIR="${pkgdir}" ninja install
}

# vim: ts=2 sw=2 et:
