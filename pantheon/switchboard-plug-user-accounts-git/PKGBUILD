# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-user-accounts
pkgver=2.2.1~61~g34e1858
pkgrel=1
pkgdesc='Switchboard User Accounts Plug'
arch=('x86_64')
url='https://github.com/elementary/switchboard-plug-useraccounts'
license=('GPL3')
groups=('pantheon-unstable')
depends=('accountsservice' 'cairo' 'gdk-pixbuf2' 'glib2' 'glibc'
         'gnome-desktop' 'gtk3' 'libgee' 'libpwquality' 'polkit'
         'libgranite.so' 'libswitchboard-2.0.so')
makedepends=('meson' 'git' 'gobject-introspection' 'granite'
             'switchboard' 'vala')
source=("switchboard-plug-user-accounts::git+https://github.com/elementary/switchboard-plug-useraccounts.git")
sha256sums=('SKIP')

pkgver() {
  cd switchboard-plug-user-accounts
  echo "$(git describe HEAD --tags | sed 's|-|~|g')"
}

build() {
  mkdir -p build
  cd build
  arch-meson ../switchboard-plug-user-accounts
  ninja
}

package() {
  cd build
   DESTDIR="${pkgdir}" ninja install
}

# vim: ts=2 sw=2 et:
