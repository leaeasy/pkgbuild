# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evolution-data-server
pkgver=3.32.1
pkgrel=1
pkgdesc="Centralized access to appointments and contacts"
url="https://wiki.gnome.org/Apps/Evolution"
arch=(x86_64)
depends=(nss krb5 libical db)
makedepends=(intltool gperf gobject-introspection vala gtk-doc boost git cmake ninja)
license=(GPL)
_commit=3.32.1
source=("git+https://git.gnome.org/browse/evolution-data-server#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^EVOLUTION_DATA_SERVER_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  cd $pkgname

  # http://site.icu-project.org/download/61#TOC-Migration-Issues
  CXXFLAGS+=' -DU_USING_ICU_NAMESPACE=1'

  cmake . -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBEXEC_INSTALL_DIR=/usr/lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DENABLE_INTROSPECTION=ON -DENABLE_OAUTH2=OFF -DWITH_OPENLDAP=OFF \
    -DENABLE_VALA_BINDINGS=ON  -DENABLE_INSTALLED_TESTS=OFF \
	-DENABLE_GTK=OFF -DENABLE_UOA=OFF -DENABLE_GTK_DOC=OFF \
    -DWITH_PHONENUMBER=OFF -DENABLE_GOA=OFF -DENABLE_WEATHER=OFF -DENABLE_GOOGLE=OFF
  ninja
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" ninja install
}
