# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-applications-menu
pkgver=2.1.3
pkgrel=1
pkgdesc='The Pantheon Application Menu'
arch=('x86_64')
url='https://github.com/elementary/applications-menu'
license=('GPL3')
groups=('pantheon')
depends=('appstream' 'gdk-pixbuf2' 'glib2' 'glibc' 'gnome-menus' 'gtk3'
         'json-glib' 'libgee' 'libsoup' 'plank' 'zeitgeist'
         'libgranite.so' 'libswitchboard-2.0.so' 'libwingpanel-2.0.so')
makedepends=('cmake' 'switchboard' 'vala' 'wingpanel')
provides=('pantheon-applications-menu')
conflicts=('pantheon-applications-menu' 'slingshot-launcher')
replaces=('slingshot-launcher')
source=("pantheon-applications-menu-${pkgver}.tar.gz::https://github.com/elementary/applications-menu/archive/${pkgver}.tar.gz")
sha256sums=('1d9f9be5faeac74da5f2f2e2c9e9f61f644737ed8703bb2ec6716e63d0832920')

prepare() {
  cd applications-menu-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd applications-menu-${pkgver}/build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
    -DGSETTINGS_COMPILE='OFF' \
    -DUSE_UNITY='OFF'
  make
}

package() {
  cd applications-menu-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
