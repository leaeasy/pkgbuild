# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-photos
pkgver=r3907.47ed37bc
pkgrel=1
pkgdesc='The Pantheon Photos Manager'
arch=('x86_64')
url='https://github.com/elementary/photos'
license=('LGPL2.1')
groups=('pantheon-unstable')
depends=('cairo' 'gdk-pixbuf2' 'geocode-glib' 'glib2' 'glibc'
         'gst-plugins-base-libs' 'gstreamer' 'gtk3' 'json-glib' 'libexif'
         'libgee' 'libgexiv2' 'libgphoto2' 'libraw' 'libsoup' 'libxml2' 'pango'
         'rest' 'sqlite' 'webkit2gtk'
         'libgranite.so.5' 'libgudev-1.0.so')
makedepends=('cmake' 'git' 'granite' 'intltool' 'vala')
source=('pantheon-photos::git+https://github.com/elementary/photos.git')
sha256sums=('SKIP')

pkgver() {
  cd pantheon-photos
  echo "$(git describe HEAD --tags | sed 's|-|~|g')"
}

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  arch-meson ../pantheon-photos
  ninja
}

package() {
  cd build

  DESTDIR="${pkgdir}" ninja install
}
# vim: ts=2 sw=2 et:
