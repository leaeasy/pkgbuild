# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=cerbere
pkgver=0.2.4
pkgrel=1
pkgdesc='The Pantheon Watchdog'
arch=('x86_64')
url='https://github.com/elementary/cerbere'
license=('GPL2')
groups=('pantheon-unstable')
depends=('dconf' 'glib2' 'glibc' 'libgee')
makedepends=('cmake' 'git' 'vala')
provides=('cerbere')
conflicts=('cerbere')
replaces=('cerbere-bzr')
source=('git+https://github.com/elementary/cerbere.git')
sha256sums=('SKIP')

pkgver() {
  cd cerbere
  echo "$(git describe HEAD --tags | sed 's|-|~|g')"
}

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  arch-meson ../cerbere
  ninja
}

package() {
  cd build

  DESTDIR="${pkgdir}" ninja install
}
