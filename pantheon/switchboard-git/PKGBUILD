# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard
pkgver=2.3.2~21~g0fc05c5
pkgrel=1
pkgdesc='The Pantheon Control Center'
arch=('x86_64')
url='https://github.com/elementary/switchboard'
license=('GPL3')
groups=('pantheon-unstable')
depends=('clutter-gtk' 'glib2' 'glibc' 'gtk3' 'libgee'
         'granite')
makedepends=('cmake' 'git' 'granite' 'intltool' 'vala')
optdepends=('switchboard-plug-about-bzr: About plug'
            'switchboard-plug-default-applications-bzr: Default applications plug'
            'switchboard-plug-elementary-tweaks-bzr: Elementary tweaks plug'
            'switchboard-plug-keyboard-bzr: Keyboard plug'
            'switchboard-plug-pantheon-shell-bzr: Pantheon Shell plug'
            'switchboard-plug-power-bzr: Power plug')
provides=('libswitchboard-2.0.so')
source=('git+https://github.com/elementary/switchboard.git')
sha256sums=('SKIP')

pkgver() {
  cd switchboard
  echo "$(git describe HEAD --tags | sed 's|-|~|g')"
}

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  arch-meson ../switchboard -D libunity=false
  ninja
}

package() {
  cd build

  DESTDIR="${pkgdir}" ninja install
}

