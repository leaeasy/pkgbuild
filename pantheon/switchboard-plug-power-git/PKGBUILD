# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-power
pkgver=2.3.5+r766
pkgrel=1
pkgdesc='Switchboard Power Plug'
arch=('x86_64')
url='https://github.com/elementary/switchboard-plug-power'
license=('GPL3')
groups=('pantheon-unstable')
depends=('glib2' 'glibc' 'gnome-settings-daemon' 'gtk3' 'libgee' 'polkit'
         'libswitchboard-2.0.so')
makedepends=('git' 'granite' 'meson' 'switchboard' 'vala')
provides=('switchboard-plug-power')
conflicts=('switchboard-plug-power')
source=('git+https://github.com/elementary/switchboard-plug-power.git')
sha256sums=('SKIP')

pkgver() {
  cd switchboard-plug-power

  echo "2.3.5+r$(git rev-list --count HEAD)"
}

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  arch-meson ../switchboard-plug-power
  ninja
}

package() {
  cd build

  DESTDIR="${pkgdir}" ninja install
}

# vim: ts=2 sw=2 et:
