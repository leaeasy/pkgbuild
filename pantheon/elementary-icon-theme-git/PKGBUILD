# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=elementary-icon-theme
pkgver=4.3.1~157~gc8054634
pkgrel=1
pkgdesc='Named, vector icons for elementary OS'
arch=('any')
url='https://github.com/elementary/icons'
license=('GPL3')
groups=('pantheon-unstable')
depends=('gtk-update-icon-cache' 'hicolor-icon-theme')
makedepends=('cmake' 'git')
options=('!emptydirs')
source=('elementary-icon-theme::git+https://github.com/elementary/icons.git')
sha256sums=('SKIP')

pkgver() {
  cd elementary-icon-theme

  echo "$(git describe HEAD --tags | sed 's|-|~|g')"
}

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

package() {
  cd build

  cmake ../elementary-icon-theme \
    -DCMAKE_INSTALL_PREFIX='/usr'
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:

