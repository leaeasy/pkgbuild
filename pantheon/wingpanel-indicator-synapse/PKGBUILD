# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-synapse
pkgver=v0.1.12~8~g3544dd6
pkgrel=1
pkgdesc='Synapse indicator for Wingpanel'
arch=('x86_64')
url='https://github.com/tom95/indicator-synapse'
license=('GPL2')
groups=('pantheon-unstable')
depends=('gdk-pixbuf2' 'glib2' 'glibc' 'gtk3' 'libkeybinder3'
         'libgranite.so' 'libwingpanel-2.0.so')
makedepends=('git' 'gobject-introspection' 'granite' 'meson' 'vala'
             'wingpanel')
source=('git+https://github.com/tom95/indicator-synapse.git')
sha256sums=('SKIP')

pkgver() {
  cd indicator-synapse
  echo "$(git describe HEAD --tags | sed 's|-|~|g')"
}

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib ../indicator-synapse/
  make
}

package() {
  cd build
  DESTDIR="${pkgdir}" make install
}

# vim: ts=2 sw=2 et:
