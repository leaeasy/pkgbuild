# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-files
pkgver=4.1.8
pkgrel=2
pkgdesc='The Pantheon File Browser'
arch=(x86_64)
url='https://github.com/elementary/files'
license=(GPL3)
groups=(pantheon)
depends=(
  atk
  cairo
  gdk-pixbuf2
  glib2
  gtk3
  libcanberra
  libgee
  libgranite.so
  libnotify
  pango
  sqlite
  zeitgeist
)
makedepends=(
  meson
  git
  gnome-common
  ninja
  vala
)
optdepends=(
  'contractor: Various context menu entries'
  'gvfs: Cleaner devices section'
  'tumbler: Thumbnails generation'
)
source=(pantheon-files::git+https://github.com/elementary/files.git#tag=${pkgver})
sha256sums=(SKIP)

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  arch-meson ../pantheon-files -Dwith-unity=false
  ninja
}

package() {
  cd build
  DESTDIR="${pkgdir}" ninja install
}

# vim: ts=2 sw=2 et:
