# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-code
pkgver=r3071.4d0fc620
pkgrel=1
pkgdesc='The Pantheon Code Editor'
arch=('x86_64')
url='https://github.com/elementary/scratch'
license=('GPL3')
groups=('pantheon-unstable')
depends=('cairo' 'glib2' 'glibc' 'gtk3' 'gtksourceview3' 'libgee' 'libpeas'
         'libsoup' 'pango' 'zeitgeist'
         'libgranite.so')
makedepends=('appstream' 'git' 'gobject-introspection' 'granite'
             'gtkspell3' 'intltool' 'meson' 'vala' 'vte3' 'webkit2gtk')
optdepends=('gtkspell3: Spell Check extension'
            'vala: Outline extension'
            'vte3: Terminal extension'
            'webkit2gtk: Browser Preview extension')
source=('pantheon-code::git+https://github.com/elementary/code.git')
sha256sums=('SKIP')

pkgver() {
  cd pantheon-code

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build

  sed 's/libvala-0.36/libvala-0.38/' -i pantheon-code/plugins/outline/meson.build
}

build() {
  cd build

  arch-meson ../pantheon-code
  ninja
}

package() {
  cd build

  DESTDIR="${pkgdir}" ninja install
}

# vim: ts=2 sw=2 et:
