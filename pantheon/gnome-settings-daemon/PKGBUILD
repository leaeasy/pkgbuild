# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-settings-daemon-elementary
_pkgname=gnome-settings-daemon
pkgver=3.32.0
pkgrel=1
pkgdesc="GNOME Settings Daemon"
url="https://git.gnome.org/browse/gnome-settings-daemon"
arch=(x86_64)
license=(GPL)
depends=(dconf gnome-desktop gsettings-desktop-schemas libcanberra-pulse libnotify libsystemd
         pulseaudio pulseaudio-alsa upower librsvg geocode-glib geoclue2
         libgudev)
provides=(gnome-settings-daemon)
conflicts=(gnome-settings-daemon)
makedepends=(libxslt docbook-xsl python git meson)

groups=(gnome)
_commit=9381b6c2be1ae0e58a4adb5375f59e6765dd0276
source=("git+https://gitlab.gnome.org/GNOME/gnome-settings-daemon.git#commit=$_commit"
        "git+https://git.gnome.org/browse/libgnome-volume-control"
		"0001-remove-unused-plugin.patch"
		"manage-dpms.patch"
		)
sha256sums=('SKIP' 'SKIP' 'SKIP' 'SKIP')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/^GNOME_SETTINGS_DAEMON_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $_pkgname

  git submodule init
  git config --local submodule.subprojects/gvc.url "$srcdir/libgnome-volume-control"
  git submodule update
  patch -Np1 -i ${srcdir}/0001-remove-unused-plugin.patch
  patch -Np1 -i ${srcdir}/manage-dpms.patch
}

build() {
	meson --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
		--libexecdir=/usr/lib/gnome-settings-daemon \
		$_pkgname build -Dcups=false -Dsmartcard=false  -Dwayland=false
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
