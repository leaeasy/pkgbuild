# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-session
pkgver=2018.05.10
pkgrel=1
pkgdesc='Session settings for Pantheon'
arch=('any')
url='https://github.com/elementary/session-settings'
license=('GPL3')
groups=('pantheon-unstable')
depends=('cerbere' 'dconf' 'gala' 'gconf' 'gnome-keyring'
         'gnome-session' 'pantheon-applications-menu'
         'pantheon-dpms-helper' 'plank' 'wingpanel')
makedepends=('git')
optdepends=('pantheon-default-settings-git')
source=('pantheon-session::git+https://github.com/elementary/session-settings.git')
sha256sums=('SKIP')

pkgver() {
  cd pantheon-session
  echo "2018.05.10"
}

prepare() {
  cd pantheon-session/gnome-session

  sed 's/gnome-settings-daemon//' -i *
}

package() {
  cd pantheon-session

  mkdir -p "${pkgdir}"/{etc/xdg,usr/share/{gnome-session,pantheon}}
  cp -dr --no-preserve='ownership' applications "${pkgdir}"/usr/share/pantheon/
  cp -dr --no-preserve='ownership' autostart "${pkgdir}"/etc/xdg/
  cp -dr --no-preserve='ownership' gnome-session "${pkgdir}"/usr/share/gnome-session/sessions
  cp -dr --no-preserve='ownership' wayland-sessions "${pkgdir}"/usr/share/
  cp -dr --no-preserve='ownership' xsessions "${pkgdir}"/usr/share/

  find "${pkgdir}" -type d -exec chmod 755 {} +
  find "${pkgdir}" -type f -exec chmod 644 {} +
}

# vim: ts=2 sw=2 et:
